
from os.path import join
from itertools import product

configfile: "config.yaml"


def param_dict_to_list(param_dict):
    ks = sorted(list(param_dict.keys()))
    vs = [param_dict[k] for k in ks]
    all_kvs = [zip(ks, combo) for combo in product(*vs)]
    return [":".join(["{}={}".format(pair[0],pair[1]) for pair in kv]) for kv in all_kvs]


# Matrix factorization params
####################################
# Define some useful directories
####################################

## Data directories
TCGA_DIR = config["tcga_dir"]
REACTOME_DIR = config["reactome_dir"]

RESULT_DIR = config["result_dir"]
## Simulation study directories
SIM_DIR = join(RESULT_DIR, "simulation_study")
SIM_SCHEMA_DIR = join(SIM_DIR, "data_schema")
SIM_DATA_DIR = join(SIM_DIR, "data_hdfs")
SIM_PWY_DIR = join(SIM_DIR, "pathway_jsons")
SIM_PARAM_DIR = join(SIM_DIR, "true_parameters")
SIM_INFER_DIR = join(SIM_DIR, "inferred_parameters")
SIM_IMPUTE_DIR = join(SIM_DIR, "imputed_data")
SIM_HISTORY_DIR = join(SIM_DIR, "training_histories")
SIM_SCORE_DIR = join(SIM_DIR, "scores")
SIM_VIS_DIR = join(SIM_DIR, "visualizations")
SIM_SCATTER_DIR = join(SIM_VIS_DIR, "embedding_scatter")
SIM_FACTORPLOT_DIR = join(SIM_VIS_DIR, "factor_plots")

## TCGA run directories
EXP_DIR = join(RESULT_DIR, "real_data_runs")
EXP_INFER_DIR = join(EXP_DIR, "inferred_parameters")
EXP_HISTORY_DIR = join(EXP_DIR, "training_histories")
EXP_VIS_DIR = join(EXP_DIR, "visualizations")
EXP_SCATTER_DIR = join(EXP_VIS_DIR, "embedding_scatter")
EXP_FACTORPLOT_DIR = join(EXP_VIS_DIR, "factor_plots")


####################################
# Downloading data
####################################
REACTOME_URL = config["reactome_url"]


#####################################
# Unpack pathway set parameters
#####################################
SIM_PATHWAY_PARAM_DICT = config["sim_pathway_params"]
SIM_PATHWAY_PARAMS = param_dict_to_list(SIM_PATHWAY_PARAM_DICT) 

EXP_PATHWAY_PARAM_DICT = config["tcga_pathway_params"]
EXP_PATHWAY_PARAMS = param_dict_to_list(EXP_PATHWAY_PARAM_DICT) 


#######################################
# Unpack pathway corruption parameters
#######################################
PATHWAY_CORR_REPS = list(range(config["pathway_corruption_replicates"]))
PATHWAY_CORRUPTION_PARAMS = config["pathway_corruption_params"]
PATHWAY_ADDK = PATHWAY_CORRUPTION_PARAMS["addk"]
PATHWAY_REMOVEK = PATHWAY_CORRUPTION_PARAMS["removek"]
PATHWAY_ADDNODE = PATHWAY_CORRUPTION_PARAMS["addnode"]
PATHWAY_REMOVENODE = PATHWAY_CORRUPTION_PARAMS["removenode"]

CORRUPTION_PARAMS = [f"kadd={ka}_krem={kr}_nadd={na}_nrem={nr}" for ka in PATHWAY_ADDK\
                                                                for kr in PATHWAY_REMOVEK\
                                                                for na in PATHWAY_ADDNODE\
                                                                for nr in PATHWAY_REMOVENODE]
CORRUPTION_PARAMS += ["original=1", "randomized=1"]

####################################
# Unpack data simulation parameters
####################################
SIM_REPS = list(range(config["sim_replicates"]))
SIM_DATA_PARAMS = config["sim_data_params"]
SIM_SNR = SIM_DATA_PARAMS["snr"]
SIM_MISSING = SIM_DATA_PARAMS["missing"]
SIM_M = SIM_DATA_PARAMS["M"]
SIM_K = SIM_DATA_PARAMS["K"]
SIM_N = SIM_DATA_PARAMS["N"]
SIM_MKN = [f"m={m}_k={k}_n={n}" for m,k,n in zip(SIM_M, SIM_K, SIM_N)]

##################################
# Simulation Study Method parameters
##################################
SIM_METHOD_PARAMS = config["sim_method_params"]
SIM_METHODS = list(SIM_METHOD_PARAMS.keys())

SIM_MATFAC_PARAM_DICTS = SIM_METHOD_PARAMS["matfac"]
SIM_MATFAC_PARAMS = param_dict_to_list(SIM_MATFAC_PARAM_DICTS)

###################################
# TCGA data preprocessing parameters
###################################
EXP_TCGA_PARAMS = config["tcga_data_params"]
EXP_HELDOUT_CTYPES = EXP_TCGA_PARAMS["heldout_ctypes"]
EXP_DROPCTYPES_STR = ":".join(EXP_HELDOUT_CTYPES)


###################################################################################################
# RULES
###################################################################################################

rule all:
    input:
       #sim_inferred=expand(join(SIM_INFER_DIR,"method={method}/{mkn}/snr={snr}/srep={srep}/{corruption}/crep={crep}.hdf"),
       #                    method=METHODS, mkn=SIM_MKN, snr=SIM_SNR, srep=SIM_REPS,
       #                    corruption=CORRUPTION_PARAMS, crep=PATHWAY_CORR_REPS),
       sim_score_table=join(SIM_SCORE_DIR, "sim_scores.tsv"),
       #sim_snr_lineplot=join(SIM_VIS_DIR, "snr-lineplot.png"),
       #sim_snr_heatmap=join(SIM_VIS_DIR, "snr-heatmap.png")
       #sim_scatters=expand(join(SIM_SCATTER_DIR,"usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.html"),
       #                    usedk=SIM_USED_K, truek=SIM_TRUE_K, snr=SIM_SNR, rep=SIM_REPS),
       #sim_factorplots=expand(join(SIM_FACTORPLOT_DIR,"method={method}/{mkn}_snr={snr}/srep={srep}/{corruption}/crep={crep}.html"),
       #                       method=METHODS, mkn=SIM_MKN, snr=SIM_SNR, srep=SIM_REPS,
       #                       corruption=CORRUPTION_PARAMS, crep=PATHWAY_CORR_REPS),
       #exp_inferred=expand(join(EXP_INFER_DIR,"usedk={usedk}.hdf"),
       #                    usedk=EXP_USED_K),
       #exp_scatters=expand(join(EXP_SCATTER_DIR,"usedk={usedk}.html"),
       #                    usedk=EXP_USED_K),
       #exp_factorplots=expand(join(EXP_FACTORPLOT_DIR,"usedk={usedk}.html"),
       #                       usedk=EXP_USED_K)


###########################
# Runs on real (TCGA) data
###########################

rule exp_factor_plot:
    input:
        src="scripts/vis_pathway_factor_plot.py",
        params_hdf=join(EXP_INFER_DIR, "usedk={usedk}.hdf"),
        pwy_json=join(SIM_PWY_DIR, "usedk={usedk}.json")
    output:
        factorplot=join(EXP_FACTORPLOT_DIR,"usedk={usedk}.html")
    params:
        plottedk=lambda wc: int(wc["usedk"])//5
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.pwy_json} {output.factorplot} {params.plottedk}"

rule exp_embedding_scatter:
    input:
        src="scripts/vis_embedding_scatter.py",
        clinical_hdf=join(TCGA_DIR, "tcga_clinical.hdf"),
        params_hdf=join(EXP_INFER_DIR, "usedk={usedk}.hdf")
    output:
        scatter_html=join(EXP_SCATTER_DIR, "usedk={usedk}.html")
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.clinical_hdf} 1 {output.scatter_html}"


rule exp_run_matfac:
    input:
        src="scripts/run_matfac.jl",
        status_file="gpu_status.txt",
        data=join(TCGA_DIR, "tcga_omic_prep_dropctypes="+EXP_DROPCTYPES_STR+".hdf"),
        pwy_json=join(REACTOME_DIR, "{pwy_params}.json"),
    output:
        inferred_bson=join(EXP_INFER_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}.bson"),
        inferred_hdf=join(EXP_INFER_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}.hdf"),
        history_json=join(EXP_HISTORY_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}.json")
    resources:
        mem_mb=10000,
        gpu=1
    params:
        matfac_kwargs=lambda wc: " ".join(wc["method_params"].split(":"))
    shell:
        "julia --project=. -t 1 {input.src} {input.data} {input.pwy_json} {output.inferred_bson} {output.inferred_hdf} {params.matfac_kwargs} history_json={output.history_json} gpu_status_file={input.status_file}"


###########################
# Simulation study
###########################

rule vis_snr:
    input:
        src="scripts/vis_snr.py",
        scores_tsv=join(SIM_SCORE_DIR, "sim_scores.tsv")
    output:
        lineplot=join(SIM_VIS_DIR, "snr-lineplot.png"),
        heatmap=join(SIM_VIS_DIR, "snr-heatmap.png")
    shell:
        "python {input.src} {input.scores_tsv} {output.lineplot} {output.heatmap} X_pwy_spearman_corr"


rule factor_plot:
    input:
        src="scripts/vis_pathway_factor_plot.py",
        params_hdf=join(SIM_INFER_DIR, "method=matfac/{method_params}/{pwy_params}/{schema_params}/snr={snr}/srep={srep}/{corruption}/crep={crep}.hdf"),
        true_pwy_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{schema_params}/srep={srep}/pathways.json"),
        used_pwy_json=join(SIM_PWY_DIR, "{pwy_params}/{schema_params}/snr={snr}/srep={srep}/{corruption}/crep={crep}.json")
    output:
        factorplot=join(SIM_FACTORPLOT_DIR, "method=matfac/{method_params,[_.=:a-zA-Z0-9]+}/{pwy_params,[_=a-z0-9]+}/{schema_params,[_.=a-z0-9]+}/snr={snr,[.0-9]+}/srep={srep,[0-9]+}/{corruption,[_.=a-z0-9]+}/crep={crep,[0-9]+}.html")
    #params:
        #plottedk=lambda wc: int(wc["k"])
	#plottedk=lambda wc: 5
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.true_pwy_json} {input.used_pwy_json} {output.factorplot}"

#TODO
#rule embedding_scatter:
#    input:
#        src="scripts/vis_embedding_scatter.py",
#        clinical_hdf=join(TCGA_DIR, "tcga_clinical.hdf"),
#        params_hdf=join(SIM_INFER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.hdf")
#    output:
#        scatter_html=join(SIM_SCATTER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.html")
#    resources:
#        mem_mb=5000
#    shell:
#        "python {input.src} {input.params_hdf} {input.clinical_hdf} 0 {output.scatter_html}"


rule tabulate_scores:
    input:
        src="scripts/tabulate_scores.py",
        infer_scores=expand(join(SIM_SCORE_DIR, "method={method}/{method_params}/{pwy_params}/{mkn}/snr={snr}/missing={missing}/srep={srep}/{corruption}/crep={crep}_infer.json"),
                         method=SIM_METHODS, method_params=SIM_MATFAC_PARAMS, pwy_params=SIM_PATHWAY_PARAMS, 
                         mkn=SIM_MKN, snr=SIM_SNR, missing=SIM_MISSING, srep=SIM_REPS,
                         corruption=CORRUPTION_PARAMS, crep=PATHWAY_CORR_REPS),
        impute_scores=expand(join(SIM_SCORE_DIR, "method={method}/{method_params}/{pwy_params}/{mkn}/snr={snr}/missing={missing}/srep={srep}/{corruption}/crep={crep}_impute.json"),
                         method=SIM_METHODS, method_params=SIM_MATFAC_PARAMS, pwy_params=SIM_PATHWAY_PARAMS, 
                         mkn=SIM_MKN, snr=SIM_SNR, missing=SIM_MISSING, srep=SIM_REPS,
                         corruption=CORRUPTION_PARAMS, crep=PATHWAY_CORR_REPS)
    output:
        score_tsv=join(SIM_SCORE_DIR, "sim_scores.tsv")
    resources:
        mem_mb=1000
    shell:
        "python {input.src} {output.score_tsv} {input.infer_scores} {input.impute_scores}"


rule score_matfac_impute:
    input:
        src="scripts/score_matfac_impute.py",
        imputed_hdf=join(SIM_IMPUTE_DIR, "method=matfac", "{method_params}", "{pwy_params}", "{schema_params}", "snr={snr}", "missing={missing}", "srep={srep}", "{corruption_params}/crep={crep}.hdf"),
        masked_hdf=join(SIM_DATA_DIR, "{pwy_params}/{sim_params}/snr={snr}", "missing={missing}", "srep={srep}.hdf")
        true_hdf=join(SIM_DATA_DIR, "{pwy_params}/{sim_params}/snr={snr}", "srep={srep}.hdf"),
    output:
        score_json=join(SIM_SCORE_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}", "{schema_params,[_.=a-z0-9]+}", "snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}", "srep={srep,[0-9]+}", "{corruption_params,[_.=a-z0-9]+}", "crep={crep,[0-9]+}_task=impute.json")
    shell:
        "python {input.src} {input.imputed_hdf} {input.true_hdf} {output.score_json}"


rule sim_matfac_impute:
    input:
        src="scripts/impute_matfac.jl",
        model_bson=join(SIM_INFER_DIR, "method=matfac", "{method_params}", "{pwy_params}", "{schema_params}", "snr={snr}", "missing={missing}", "srep={srep}", "{corruption_params}/crep={crep}.bson"),
    output:
        imputed_hdf=join(SIM_IMPUTE_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}", "{schema_params,[_.=a-z0-9]+}", "snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}", "srep={srep,[0-9]+}", "{corruption_params, [_.=a-z0-9]+}/crep={crep, [0-9]+}.hdf"),
    shell:
        "julia --project=. -t 1 {input.src} {input.model_bson} {output.imputed_hdf}"


rule score_matfac:
    input:
        src="scripts/score_matfac.py",
        true_params=join(SIM_PARAM_DIR, "{pwy_params}", "{schema_params}", "snr={snr}", "srep={srep}.hdf"),
        pred_params=join(SIM_INFER_DIR, "method=matfac", "{method_params}", "{pwy_params}", "{schema_params}", "snr={snr}", "missing={missing}", "srep={srep}", "{corruption_params}", "crep={crep}.hdf")
    output:
        score_json=join(SIM_SCORE_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}", "{schema_params,[_.=a-z0-9]+}", "snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}", "srep={srep,[0-9]+}", "{corruption_params,[_.=a-z0-9]+}", "crep={crep,[0-9]+}_task=infer.json")
    resources:
        #mem_mb=1000
        mem_mb=100
    shell:
        "python {input.src} {input.true_params} {input.pred_params} {output.score_json}" 


rule sim_run_matfac:
    input:
        src="scripts/run_matfac.jl",
        status_file="gpu_status.txt",
        data=join(SIM_DATA_DIR, "{pwy_params}/{schema_params}/snr={snr}/missing={missing}/srep={srep}.hdf"),
        pwy_json=join(SIM_PWY_DIR, "{pwy_params}/{schema_params}/srep={srep}/{corruption_params}/crep={crep}.json")
    output:
        inferred_bson=join(SIM_INFER_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}", "{schema_params,[_.=a-z0-9]+}", "snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}", "srep={srep,[0-9]+}", "{corruption_params, [_.=a-z0-9]+}/crep={crep, [0-9]+}.bson"),
        inferred_hdf=join(SIM_INFER_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}", "{schema_params,[_.=a-z0-9]+}", "snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}","srep={srep, [0-9]+}", "{corruption_params, [_.=a-z0-9]+}/crep={crep, [0-9]+}.hdf"),
        history_json=join(SIM_HISTORY_DIR, "method=matfac", "{method_params,[_.=:a-zA-Z0-9]+}", "{pwy_params,[_=a-z0-9]+}", "{schema_params,[_.=a-z0-9]+}", "snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}", "srep={srep, [0-9]+}", "{corruption_params, [_.=a-z0-9]+}/crep={crep, [0-9]+}.json")
    resources:
        mem_mb=1000
        #mem_mb=10000,
        #gpu=1
    params:
        matfac_kwargs=lambda wc: " ".join(wc["method_params"].split(":"))
    shell:
        "julia --project=. -t 1 {input.src} {input.data} {input.pwy_json} {output.inferred_bson} {output.inferred_hdf} {params.matfac_kwargs} history_json={output.history_json} gpu_status_file={input.status_file}"


rule randomize_pathway_edges:
    input:
        src=join("scripts","randomize_pathway_edges.py"),
        pwy_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{schema_params}/srep={srep}/pathways.json"),
    output:
        randomized_json=join(SIM_PWY_DIR, "{pwy_params,[._:=a-z0-9]+}/{schema_params,[_.=a-z0-9]+}/srep={srep,[0-9]+}/randomized=1/crep={crep,[0-9]+}.json")
    resources:
        mem_mb=500
    shell:
        "python {input.src} {input.pwy_json} {output.randomized_json}"


rule mask_data:
    input:
        src=join("scripts", "mask_data.py"),
        data_hdf=join(SIM_DATA_DIR, "{pwy_params}/{sim_params}/snr={snr}", "srep={srep}.hdf")
    output:
        masked_hdf=join(SIM_DATA_DIR, "{pwy_params,[_=a-z0-9]+}/{sim_params,[._=a-z0-9]+}/snr={snr,[.0-9]+}", "missing={missing,[.0-9]+}", "srep={srep,[0-9]+}.hdf")
    shell:
        "python {input.src} {input.data_hdf} {output.masked_hdf} each={wildcards.missing}"


rule corrupt_pathways:
    input:
        src="scripts/corrupt_pathways.py",
        pwy_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{schema_params}/srep={srep}/pathways.json"),
        ranked_json=join(REACTOME_DIR, "reactome_ranked.json"),
        features_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{schema_params}/srep={srep}/features.json")
    output:
        corr_json=join(SIM_PWY_DIR, "{pwy_params,[_=a-z0-9]+}/{schema_params,[_.=a-z0-9]+}/srep={srep,[0-9]+}/kadd={kadd,[.0-9]+}_krem={krem,[.0-9]+}_nadd={nadd,[.0-9]+}_nrem={nrem,[.0-9]+}/crep={crep,[0-9]+}.json")
    shell:
        "python {input.src} {input.pwy_json} {input.ranked_json} {input.features_json} {wildcards.kadd} {wildcards.krem} {wildcards.nadd} {wildcards.nrem} {output.corr_json}"


rule copy_pathways:
    input:
        pwy_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{schema_params}/srep={srep}/pathways.json"),
    output:
        cp_json=join(SIM_PWY_DIR, "{pwy_params,[_=a-z0-9]+}/{schema_params,[_.=a-z0-9]+}/srep={srep,[0-9]+}/original=1/crep={crep,[0-9]+}.json")
    shell:
        "cp {input.pwy_json} {output.cp_json}"        


rule simulate_data:
    input:
        src="scripts/simulate_data.jl",
        pwy_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{sim_params}/srep={srep}/pathways.json"), 
        sample_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{sim_params}/srep={srep}/samples.json"),
        feature_json=join(SIM_SCHEMA_DIR, "{pwy_params}/{sim_params}/srep={srep}/features.json")
    output:
        data_hdf=join(SIM_DATA_DIR, "{pwy_params,[_=a-z0-9]+}/{sim_params,[._=a-z0-9]+}/snr={snr,[.0-9]+}", "srep={srep,[0-9]+}.hdf"),
        param_hdf=join(SIM_PARAM_DIR,"{pwy_params,[_=a-z0-9]+}/{sim_params,[._=a-z0-9]+}/snr={snr,[.0-9]+}", "srep={srep,[0-9]+}.hdf")
    resources:
        #mem_mb=10000
        mem_mb=1000
    shell:
        "julia --project=. -t 1 {input.src} {input.pwy_json} {input.sample_json} {input.feature_json} {output.data_hdf} {output.param_hdf} snr={wildcards.snr}"
   

rule prep_sim_inputs:
    input:
        src="scripts/prep_simulation_inputs.py",
        full_pwy_json=join(REACTOME_DIR, "topk={topk}.json"),
        full_samples_json=join(TCGA_DIR, "tcga_samples_dropctypes=.json"),
        full_features_json=join(TCGA_DIR, "tcga_features_dropctypes=.json")
    output:
        pwy_json=join(SIM_SCHEMA_DIR, "sim_topk={topk}/m={m,[0-9]+}_k={k,[0-9]+}_n={n,[0-9]+}/srep={srep,[0-9]+}/pathways.json"), 
        samples_json=join(SIM_SCHEMA_DIR, "sim_topk={topk}/m={m,[0-9]+}_k={k,[0-9]+}_n={n,[0-9]+}/srep={srep,[0-9]+}/samples.json"),
        features_json=join(SIM_SCHEMA_DIR, "sim_topk={topk}/m={m,[0-9]+}_k={k,[0-9]+}_n={n,[0-9]+}/srep={srep,[0-9]+}/features.json")
    shell:
        "python {input.src} {input.full_pwy_json} {input.full_samples_json} {input.full_features_json} {wildcards.m} {wildcards.k} {wildcards.n} {output.pwy_json} {output.samples_json} {output.features_json}"


######################################
# Download and preprocess Reactome
######################################

rule select_topk_pwys:
    input:
        src="scripts/select_pathways.py",
        ranked_json=join(REACTOME_DIR, "reactome_ranked.json"),
    output:
        pwy_json=join(REACTOME_DIR, "topk={topk, [0-9]+}.json"),
    resources:
        mem_mb=500
    shell:
        "python {input.src} {input.ranked_json} {wildcards.topk} {output.pwy_json}"

rule rank_pwys:
    input:
        src="scripts/rank_pathways.py",
        feature_json=join(TCGA_DIR, "tcga_features_dropctypes=.json"),
        all_pwy_json=join(REACTOME_DIR, "reactome.json")
    output:
        ranked_json=join(REACTOME_DIR, "reactome_ranked.json")
    resources:
        mem_mb=500
    shell:
        "python {input.src} {input.all_pwy_json} {input.feature_json} {output.ranked_json}"


rule reactome_txt_to_json:
    input:
        src="scripts/reactome_txt_to_json.py",
        txt=join(REACTOME_DIR, "reactome.hgnc.txt"),
    output:
        json=join(REACTOME_DIR, "reactome.json")
    shell:
        "python {input.src} {input.txt} {output.json}"


rule unzip_reactome:
    input:
        zipped=join(REACTOME_DIR, "reactome.hgnc.txt.gz")
    output:
        tsv=join(REACTOME_DIR, "reactome.hgnc.txt")
    shell:
        "gunzip {input.zipped}"


rule download_reactome:
    output:
        tarball=join(REACTOME_DIR, "reactome.hgnc.txt.gz")
    shell:
        "curl {REACTOME_URL} --output {output.tarball}"


#####################################
# Preprocess TCGA
#####################################


rule extract_samples_features:
    input:
        src="scripts/extract_samples_features.py",
        tcga_hdf=join(TCGA_DIR, "tcga_omic_prep_dropctypes={dropctypes}.hdf")
    output:
        samples_json=join(TCGA_DIR, "tcga_samples_dropctypes={dropctypes,[:A-Z]*}.json"),
        features_json=join(TCGA_DIR, "tcga_features_dropctypes={dropctypes,[:A-Z]*}.json")
    shell:
        "python {input.src} {input.tcga_hdf} {output.samples_json} {output.features_json}"

rule preprocess_tcga:
    input:
        src="scripts/preprocess_tcga.py",
        tcga_hdf=join(TCGA_DIR, "tcga_omic.hdf")
    output:
        prep_hdf=join(TCGA_DIR, "tcga_omic_prep_dropctypes={dropctypes,[:A-Z]*}.hdf")
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.tcga_hdf} {output.prep_hdf} heldout_ctypes={wildcards.dropctypes} std_assays= "

rule unzip_hdf:
    input:
        zipped="data/{path}/{head}.tar.gz"
    output:
        unzipped="data/{path}/{head}.hdf"
    shell:
        "tar -xvzf {input.zipped} -C {wildcards.path}"

rule download_tcga_omic:
    output:
        tcga_omic=join(TCGA_DIR, "tcga_omic.tar.gz"),
    shell:
        "curl https://zenodo.org/record/6977490/files/tcga_omic.tar.gz?download=1 -o {output.tcga_omic}"


rule download_tcga_clinical:
    output:
        tcga_clinical=join(TCGA_DIR, "tcga_clinical.tar.gz")
    shell:
        "curl https://zenodo.org/record/6977490/files/tcga_clinical.tar.gz?download=1 -o {output.tcga_clinical}"



######################################
# GPU status
######################################
rule initialize_gpu_status:
    input:
        src=join("scripts", "init_gpu_status.jl")
    output:
        status_file="gpu_status.txt"
    shell:
        "julia --project=. -t 1 {input.src} > {output.status_file}"


