
from os.path import join

configfile: "config.yaml"

####################################
# Define some useful directories
####################################

## Data directories
TCGA_DIR = config["tcga_dir"]
REACTOME_DIR = config["reactome_dir"]

RESULT_DIR = config["result_dir"]
## Simulation study directories
SIM_DIR = join(RESULT_DIR, "simulation_study")
SIM_DATA_DIR = join(SIM_DIR, "data_hdfs")
SIM_PWY_DIR = join(SIM_DIR, "pathway_jsons")
SIM_PARAM_DIR = join(SIM_DIR, "true_parameters")
SIM_INFER_DIR = join(SIM_DIR, "inferred_parameters")
SIM_HISTORY_DIR = join(SIM_DIR, "training_histories")
SIM_SCORE_DIR = join(SIM_DIR, "scores")
SIM_VIS_DIR = join(SIM_DIR, "visualizations")
SIM_SCATTER_DIR = join(SIM_VIS_DIR, "embedding_scatter")
SIM_FACTORPLOT_DIR = join(SIM_VIS_DIR, "factor_plots")

## TCGA run directories
EXP_DIR = join(RESULT_DIR, "real_data_runs")
EXP_INFER_DIR = join(EXP_DIR, "inferred_parameters")
EXP_HISTORY_DIR = join(EXP_DIR, "training_histories")
EXP_VIS_DIR = join(EXP_DIR, "visualizations")
EXP_SCATTER_DIR = join(EXP_VIS_DIR, "embedding_scatter")
EXP_FACTORPLOT_DIR = join(EXP_VIS_DIR, "factor_plots")


####################################
# Downloading data
####################################
REACTOME_URL = config["reactome_url"]

####################################
# Unpack simulation study parameters
####################################
SIM_PARAMS = config["sim_params"]
SIM_DATA_PARAMS = SIM_PARAMS["data_params"]
SIM_HELDOUT_CTYPES = SIM_DATA_PARAMS["heldout_ctypes"]
SIM_DROPCTYPES_STR = ":".join(SIM_HELDOUT_CTYPES)
SIM_REPS = list(range(SIM_DATA_PARAMS["reps"]))
SIM_SNR = SIM_DATA_PARAMS["snr"]
SIM_TRUE_K = SIM_DATA_PARAMS["true_K"]

SIM_MATFAC_PARAMS = SIM_PARAMS["matfac_params"]
SIM_MAX_EPOCHS = SIM_MATFAC_PARAMS["max_epochs"]
SIM_LR = SIM_MATFAC_PARAMS["lr"]
SIM_LAMBDA_LAYER = SIM_MATFAC_PARAMS["lambda_layer"]
SIM_LAMBDA_X = SIM_MATFAC_PARAMS["lambda_X"]
SIM_LAMBDA_Y = SIM_MATFAC_PARAMS["init_lambda_Y"]
SIM_USED_K = SIM_MATFAC_PARAMS["used_K"]
SIM_CAPACITY = SIM_MATFAC_PARAMS["gpu_capacity"]


###################################
# Unpack TCGA run parameters
###################################
EXP_PARAMS = config["tcga_params"]
EXP_TCGA_PARAMS = EXP_PARAMS["data_params"]
EXP_HELDOUT_CTYPES = EXP_TCGA_PARAMS["heldout_ctypes"]
EXP_DROPCTYPES_STR = ":".join(EXP_HELDOUT_CTYPES)
EXP_MATFAC_PARAMS = EXP_PARAMS["matfac_params"]
EXP_MAX_EPOCHS = EXP_MATFAC_PARAMS["max_epochs"]
EXP_LR = EXP_MATFAC_PARAMS["lr"]
EXP_LAMBDA_LAYER = EXP_MATFAC_PARAMS["lambda_layer"]
EXP_LAMBDA_X = EXP_MATFAC_PARAMS["lambda_X"]
EXP_LAMBDA_Y = EXP_MATFAC_PARAMS["lambda_Y"]
EXP_USED_K = EXP_MATFAC_PARAMS["used_K"]
EXP_CAPACITY = EXP_MATFAC_PARAMS["gpu_capacity"]

###################################################################################################
# RULES
###################################################################################################


rule all:
    input:
       sim_inferred=expand(join(SIM_INFER_DIR,"usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.hdf"),
                           usedk=SIM_USED_K, truek=SIM_TRUE_K, snr=SIM_SNR, rep=SIM_REPS),
       sim_scores=expand(join(SIM_SCORE_DIR,"usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.json"),
                         usedk=SIM_USED_K, truek=SIM_TRUE_K, snr=SIM_SNR, rep=SIM_REPS),
       sim_scatters=expand(join(SIM_SCATTER_DIR,"usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.html"),
                           usedk=SIM_USED_K, truek=SIM_TRUE_K, snr=SIM_SNR, rep=SIM_REPS),
       sim_factorplots=expand(join(SIM_FACTORPLOT_DIR,"usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.html"),
                              usedk=SIM_USED_K, truek=SIM_TRUE_K, snr=SIM_SNR, rep=SIM_REPS),
       exp_inferred=expand(join(EXP_INFER_DIR,"usedk={usedk}.hdf"),
                           usedk=EXP_USED_K),
       exp_scatters=expand(join(EXP_SCATTER_DIR,"usedk={usedk}.html"),
                           usedk=EXP_USED_K),
       exp_factorplots=expand(join(EXP_FACTORPLOT_DIR,"usedk={usedk}.html"),
                              usedk=EXP_USED_K)


###########################
# Runs on real (TCGA) data
###########################

rule exp_factor_plot:
    input:
        src="scripts/vis_pathway_factor_plot.py",
        params_hdf=join(EXP_INFER_DIR, "usedk={usedk}.hdf"),
        pwy_json=join(SIM_PWY_DIR, "usedk={usedk}.json")
    output:
        factorplot=join(EXP_FACTORPLOT_DIR,"usedk={usedk}.html")
    params:
        plottedk=lambda wc: int(wc["usedk"])//5
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.pwy_json} {output.factorplot} {params.plottedk}"

rule exp_embedding_scatter:
    input:
        src="scripts/vis_embedding_scatter.py",
        clinical_hdf=join(TCGA_DIR, "tcga_clinical.hdf"),
        params_hdf=join(EXP_INFER_DIR, "usedk={usedk}.hdf")
    output:
        scatter_html=join(EXP_SCATTER_DIR, "usedk={usedk}.html")
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.clinical_hdf} 1 {output.scatter_html}"


rule exp_run_matfac:
    input:
        src="scripts/run_matfac.jl",
        status_file="gpu_status.txt",
        data=join(TCGA_DIR, "tcga_omic_prep_dropctypes="+EXP_DROPCTYPES_STR+".hdf"),
        pwy_json=join(SIM_PWY_DIR, "usedk={usedk}.json")
    output:
        inferred_bson=join(EXP_INFER_DIR, "usedk={usedk}.bson"),
        inferred_hdf=join(EXP_INFER_DIR, "usedk={usedk}.hdf"),
        history_json=join(EXP_HISTORY_DIR, "usedk={usedk}.json")
    resources:
        mem_mb=10000,
        gpu=1
    shell:
        "julia --project=. {input.src} {input.data} {input.pwy_json} {output.inferred_bson} {output.inferred_hdf} max_epochs={EXP_MAX_EPOCHS} lr={EXP_LR} lambda_layer={EXP_LAMBDA_LAYER} lambda_X={EXP_LAMBDA_X} init_lambda_Y={EXP_LAMBDA_Y} capacity={EXP_CAPACITY} history_json={output.history_json} gpu_status_file={input.status_file}"


###########################
# Simulation study
###########################

rule factor_plot:
    input:
        src="scripts/vis_pathway_factor_plot.py",
        params_hdf=join(SIM_INFER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.hdf"),
        pwy_json=join(SIM_PWY_DIR, "usedk={usedk}.json")
    output:
        factorplot=join(SIM_FACTORPLOT_DIR,"usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.html")
    params:
        plottedk=lambda wc: int(wc["usedk"])//5
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.pwy_json} {output.factorplot} {params.plottedk}"

rule embedding_scatter:
    input:
        src="scripts/vis_embedding_scatter.py",
        clinical_hdf=join(TCGA_DIR, "tcga_clinical.hdf"),
        params_hdf=join(SIM_INFER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.hdf")
    output:
        scatter_html=join(SIM_SCATTER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.html")
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.params_hdf} {input.clinical_hdf} 0 {output.scatter_html}"


rule score_matfac:
    input:
        src="scripts/score_matfac.py",
        true_params=join(SIM_PARAM_DIR, "truek={truek}_snr={snr}_rep={rep}.hdf"),
        pred_params=join(SIM_INFER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.hdf")
    output:
        score_json=join(SIM_SCORE_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.json")
    resources:
        mem_mb=2000
    shell:
        "python {input.src} {input.true_params} {input.pred_params} {output.score_json}" 

rule sim_run_matfac:
    input:
        src="scripts/run_matfac.jl",
        status_file="gpu_status.txt",
        data=join(SIM_DATA_DIR, "truek={truek}_snr={snr}_rep={rep}.hdf"),
        pwy_json=join(SIM_PWY_DIR, "usedk={usedk}.json")
    output:
        inferred_bson=join(SIM_INFER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.bson"),
        inferred_hdf=join(SIM_INFER_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.hdf"),
        history_json=join(SIM_HISTORY_DIR, "usedk={usedk}_truek={truek}_snr={snr}_rep={rep}.json")
    resources:
        mem_mb=10000,
        gpu=1
    shell:
        "julia --project=. {input.src} {input.data} {input.pwy_json} {output.inferred_bson} {output.inferred_hdf} max_epochs={SIM_MAX_EPOCHS} lr={SIM_LR} lambda_layer={SIM_LAMBDA_LAYER} lambda_X={SIM_LAMBDA_X} init_lambda_Y={SIM_LAMBDA_Y} capacity={SIM_CAPACITY} history_json={output.history_json} gpu_status_file={input.status_file}"

# NOTICE for now we have usedk==truek
rule simulate_data:
    input:
        src="scripts/simulate_data.jl",
        pwy_json=join(SIM_PWY_DIR, "usedk={truek}.json"), 
        sample_json=join(TCGA_DIR, "tcga_samples_dropctypes=.json"),
        feature_json=join(TCGA_DIR, "tcga_features_dropctypes=.json")
    output:
        data_hdf=join(SIM_DATA_DIR, "truek={truek}_snr={snr}_rep={rep}.hdf"),
        param_hdf=join(SIM_PARAM_DIR, "truek={truek}_snr={snr}_rep={rep}.hdf")
    resources:
        mem_mb=10000
    shell:
        "julia --project=. {input.src} {input.pwy_json} {input.sample_json} {input.feature_json} {output.data_hdf} {output.param_hdf} snr={wildcards.snr}"
   

rule select_used_pwys:
    input:
        src="scripts/select_pathways.py",
        feature_json=join(TCGA_DIR, "tcga_features_dropctypes=.json"),
        all_pwy_json=join(REACTOME_DIR, "reactome.json"),
    output:
        pwy_json=join(SIM_PWY_DIR, "usedk={usedk}.json"),
    resources:
        mem_mb=500
    shell:
        "python {input.src} {input.all_pwy_json} {input.feature_json} {wildcards.usedk} {output.pwy_json}"


######################################
# GPU status
######################################
rule initialize_gpu_status:
    input:
        src=join("scripts", "init_gpu_status.jl")
    output:
        status_file="gpu_status.txt"
    shell:
        "julia --project=. {input.src} > {output.status_file}"

######################################
# Download and preprocess Reactome
######################################

rule reactome_txt_to_json:
    input:
        src="scripts/reactome_txt_to_json.py",
        txt=join(REACTOME_DIR, "reactome.hgnc.txt"),
    output:
        json=join(REACTOME_DIR, "reactome.json")
    shell:
        "python {input.src} {input.txt} {output.json}"

rule unzip_reactome:
    input:
        zipped=join(REACTOME_DIR, "reactome.hgnc.txt.gz")
    output:
        tsv=join(REACTOME_DIR, "reactome.hgnc.txt")
    shell:
        "gunzip {input.zipped}"

rule download_reactome:
    output:
        tarball=join(REACTOME_DIR, "reactome.hgnc.txt.gz")
    shell:
        "curl {REACTOME_URL} --output {output.tarball}"


#####################################
# Preprocess TCGA
#####################################


rule extract_samples_features:
    input:
        src="scripts/extract_samples_features.py",
        tcga_hdf=join(TCGA_DIR, "tcga_omic_prep_dropctypes={dropctypes}.hdf")
    output:
        samples_json=join(TCGA_DIR, "tcga_samples_dropctypes={dropctypes,[:A-Z]*}.json"),
        features_json=join(TCGA_DIR, "tcga_features_dropctypes={dropctypes,[:A-Z]*}.json")
    shell:
        "python {input.src} {input.tcga_hdf} {output.samples_json} {output.features_json}"

rule preprocess_tcga:
    input:
        src="scripts/preprocess_tcga.py",
        tcga_hdf=join(TCGA_DIR, "tcga_omic.hdf")
    output:
        prep_hdf=join(TCGA_DIR, "tcga_omic_prep_dropctypes={dropctypes,[:A-Z]*}.hdf")
    resources:
        mem_mb=5000
    shell:
        "python {input.src} {input.tcga_hdf} {output.prep_hdf} heldout_ctypes={wildcards.dropctypes} std_assays= "



